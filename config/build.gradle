plugins {
    id 'java'
    id 'com.google.cloud.tools.jib' version '3.4.0'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':member')

    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'
}

tasks.named('bootJar') {
    enabled = true
}

tasks.named('jar') {
    enabled = true
}

tasks.named('test') {
    useJUnitPlatform()
}

jib {
    def imageTag = System.getenv("IMAGE_TAG")
    def serverPort = "8080"
    def activeProfile = System.getenv("ACTIVE_PROFILE")
    def dockerUsername = System.getenv("DOCKER_USERNAME")
    def dockerPassword = System.getenv("DOCKER_PASSWORD")
    from {
        image = 'amazoncorretto:17-alpine3.17-jdk'
    }
    to {
        image = 'no5ing/spring-was:latest'
        tags = ['latest', imageTag]
        auth {
            username = dockerUsername ? dockerUsername : ""
            password = dockerPassword ? dockerPassword : ""
        }
    }
    container {
        jvmFlags = [
                '-Dspring.profiles.active=' + activeProfile,
                '-Dserver.port=' + serverPort,
                '-Xms2G', '-Xmx2G',
                '-XX:+UseG1GC',
                '-XX:+UseContainerSupport',
                '-XX:+DisableExplicitGC',
                '-server'
        ]
        ports = [serverPort]
    }
}
