plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.5'
	id 'io.spring.dependency-management' version '1.1.3'
}

repositories {
	mavenCentral()
}

subprojects {
	group = 'com.oing'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = '17'

	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'jacoco'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	tasks.named('test') {
		useJUnitPlatform()
		finalizedBy 'jacocoTestReport'
	}

	jacoco {
		toolVersion = '0.8.8'
	}

	jacocoTestReport {
		reports{
			html.destination file("$buildDir/reports/jacoco/html")
			xml.destination file("$buildDir/reports/jacoco/xml")
			csv.destination file("$buildDir/reports/jacoco/csv")
		}

		finalizedBy 'jacocoTestCoverageVerification'
	}

	jacocoTestCoverageVerification {
		def Qdomains = []
		for (qPattern in '*.QA'..'*.QZ') { // qPattern = '*.QA', '*.QB', ... '*.QZ'
			Qdomains.add(qPattern + '*')
		}

		violationRules {
			rule {
				enabled = true
				element = 'CLASS'

				// 라인 커버리지 설정
				limit {
					counter = 'LINE'
					value = 'COVEREDRATIO'
					minimum = 0.30
				}

				// 브랜치 커버리지 설정
				limit {
					counter = 'BRANCH'
					value = 'COVEREDRATIO'
					minimum = 0.70
				}

				// 커버리지 체크를 제외할 클래스들
				excludes = [] + Qdomains
			}
		}
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
		implementation 'org.springdoc:springdoc-openapi-ui:1.7.0'
		implementation 'org.springframework.boot:spring-boot-starter-actuator'
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		implementation 'org.springframework.boot:spring-boot-starter-security'
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
		annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
		annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
		annotationProcessor 'org.projectlombok:lombok'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation 'org.springframework.security:spring-security-test'
	}
}

tasks.named('bootJar') {
	enabled = false
}

tasks.named('jar') {
	enabled = true
}
